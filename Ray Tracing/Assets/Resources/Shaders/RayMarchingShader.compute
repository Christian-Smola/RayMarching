//Based on the following article by Inigo Quilez
//https://iquilezles.org/articles/distfunctions/
#pragma kernel CSMain
#include "/Includes/Noise.hlsl"

RWTexture2D<float4> Result;
float4 _DirectionalLight;
float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;
int _Operation;
float _Time;

Texture2D<float4> _PlanetTexture;
SamplerState sampler_PlanetTexture;

Texture2D<float4> _PlanetHeightTexture;
SamplerState sampler_PlanetHeightTexture;

Texture2D<float4> _PlanetNormalTexture;
SamplerState sampler_PlanetNormalTexture;

Texture2D<float4> _PlanetaryRingTexture;
SamplerState sampler_PlanetaryRingTexture;

Texture2D<float4> _PlanetaryRingHeightTexture;
SamplerState sampler_PlanetaryRingHeightTexture;

Texture2D<float4> _PlanetaryRingNormalTexture;
SamplerState sampler_PlanetaryRingNormalTexture;

Texture2D<float4> _SkyboxTexture;
SamplerState sampler_SkyboxTexture;

static const float PI = 3.14159265f;
static const float MaxDist = 120;
static const float Epsilon = 0.001f;

struct Ray
{
    float3 origin;
    float3 direction;
};

struct Mesh
{
    int meshID;
    int shape;
    float3 size;
    float3 position;
};

StructuredBuffer<Mesh> Meshes;
int MeshCount;

Ray CreateRay(float3 origin, float3 direction)
{
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv)
{
    float3 origin = mul(_CameraToWorld, float4(0.0f, 0.0f, 0.0f, 1.0f)).xyz;
    float3 direction = mul(_CameraInverseProjection, float4(uv, 0.0f, 1.0f)).xyz;
    direction = mul(_CameraToWorld, float4(direction, 0.0f)).xyz;
    direction = normalize(direction);

    return CreateRay(origin, direction);
}

float2x2 Rotate(float a)
{
    float s = sin(a);
    float c = cos(a);
    return float2x2(c, -s, s, c);
}

float mix(float x, float y, float a)
{
    return x * (1 - a) + y * a;
}

float PlaneSDF(float3 pos, float3 normal, float DistFromOrigin)
{
    return dot(pos, normal) + DistFromOrigin;
}

float SphereSDF(float3 CameraPos, float3 Center, float Radius)
{
    return length(CameraPos - Center) - Radius;
}

float CubeSDF(float3 CameraPos, float3 Center, float3 Size)
{
    float3 Offset = abs(CameraPos - Center) - Size;
    float UnsignedDistance = length(max(Offset, 0));
    float DistanceInsideBox = max(max(min(Offset.x, 0), min(Offset.y, 0)), min(Offset.z, 0));
    return UnsignedDistance + DistanceInsideBox;
}

float TorusSDF(float3 pos, float2 t)
{
    float2 q = float2(length(pos.xz) - t.x, pos.y);
    return length(q) - t.y;
}

float4 opElongate(in float3 p, in float3 h)
{
    float3 q = abs(p) - h;
    return float4(max(q, 0), min(max(q.x, max(q.y, q.z)), 0));
}

float GetMeshSDF(Mesh mesh, float3 pos)
{
    if (mesh.shape == 0)
        return SphereSDF(pos, mesh.position, mesh.size.x);
    if (mesh.shape == 1)
        return CubeSDF(pos, mesh.position, mesh.size);
    if (mesh.shape == 2)
        return PlaneSDF(pos, float3(0, 1, 0), 1.5f);
    if (mesh.shape == 3)
    {
        float3 q = pos - mesh.position;
        float4 w = opElongate(q, float3(0, -mesh.size.y + 0.2f, 0));
        return w.w + TorusSDF(w.xyz, mesh.size.xy);
    }

    return MaxDist;
}

float4 Combine(float distA, float distB, float3 colorA, float3 colorB)
{
    float dist = distA;
    float3 color = colorA;
    float blendValue = 6.5f;

    if (_Operation == 0)
    {
        //Default
        if (distB < distA)
        {
            dist = distB;
            color = colorB;
        }
    }
    else if (_Operation == 1)
    {
        //Blend
        float h = clamp(0.5f + 0.5f * (distB - distA) / blendValue, 0.0f, 1.0f);
        dist = lerp(distB, distA, h) - blendValue * h * (1 - h);
        color = lerp(colorB, colorA, h);
    }
    else if (_Operation == 2)
    {
        //Union
        float h = clamp(0.5f + 0.5f * (distB - distA) / blendValue, 0.0f, 1.0f);
        dist = mix(distB, distA, h) - blendValue * h * (1 - h);
        color = lerp(colorB, colorA, h);
    }
    else if (_Operation == 3)
    {
        //Subtraction
        float h = clamp(0.5f - 0.5f * (distB + distA) / blendValue, 0.0f, 1.0f);
        dist = mix(distB, -distA, h) + blendValue * h * (1 - h);
        color = lerp(colorB, colorA, h);
    }

    return float4(color, dist);
}

float LimitedDomainRepetition(in float3 pos, in float3 spacing, in Mesh mesh)
{
    float3 q = pos - spacing * clamp(round(pos / spacing), -1, 1);
    return GetMeshSDF(mesh, q);
}

float DistToScene(float3 pos)
{
    float GlobalDist = MaxDist;

    for (int x = 0; x < MeshCount; x++)
    {
        Mesh mesh = Meshes[x];
        float MeshDist = GetMeshSDF(mesh, pos);

        if (MeshDist < GlobalDist)
            GlobalDist = MeshDist;
    }

    return GlobalDist;
}

float3 CalculateNormals(float3 pos)
{
    float3 V1 = float3(
        DistToScene(pos + float3(Epsilon, 0, 0)),
        DistToScene(pos + float3(0, Epsilon, 0)),
        DistToScene(pos + float3(0, 0, Epsilon)));

    float3 V2 = float3(
        DistToScene(pos - float3(Epsilon, 0, 0)),
        DistToScene(pos - float3(0, Epsilon, 0)),
        DistToScene(pos - float3(0, 0, Epsilon)));

    return normalize(V1 - V2);
}

float StarNoise(float4 p)
{
    float sum = 0;
    float amp = 1;
    float scale = 1;

    for (int i = 0; i < 6; i++)
    {
        sum += snoise(p * scale) * amp;
        p.w += 100.0f;
        amp *= 0.9f;
        scale *= 2;
    }

    return sum;
}

float3 BrightnessToColor(float b, int ID)
{
    float Offset = 0.25f;
    float c = 0;

    b *= Offset;

    if (ID == 0)
    {
        c = 0.5f;
        return (float3(b * b * b * b, b * b, b) / Offset) * c;
    }
    else
    {
        c = 0.4f;
        return (float3(b, b * b, b * b * b * b) / Offset) * c;
    }
}

float3 Transformations(float3 origin, Mesh mesh)
{
    //Blue star
    if (mesh.meshID == 0)
    {
        origin.xz = mul(Rotate(_Time * 4), origin.xz - mesh.position.xz) + mesh.position.xz;
        origin -= float3(7, 0, 0);
    }

    //Red dwarf star
    if (mesh.meshID == 1)
    {
        origin.xz = mul(Rotate(_Time * 4), origin.xz - mesh.position.xz) + mesh.position.xz;
        origin -= float3(-7, 0, 0);
    }

    //Both stars
    if (mesh.meshID < 2)
        origin.xz = mul(Rotate(_Time * 1.5f), origin.xz - mesh.position.xz) + mesh.position.xz;

    if (mesh.meshID > 1)
    {
        //Rotate around the star
        origin.xz = mul(Rotate(_Time * 6), origin.xz - mesh.position.xz) + mesh.position.xz;
        origin -= float3(40, 0, 0);

        //Axial tilt
        origin.xy = mul(Rotate(-0.25f), origin.xy - mesh.position.xy) + mesh.position.xy;
    }

    if (mesh.meshID == 4)
    {
        origin.xz = mul(Rotate(_Time * 15), origin.xz - mesh.position.xz) + mesh.position.xz;
        origin -= float3(10, 0, 0);
        origin.xz = mul(Rotate(_Time * 8), origin.xz - mesh.position.xz) + mesh.position.xz;
    }
    else if (mesh.meshID == 2 || mesh.meshID == 3)
    {
        origin.xz = mul(Rotate(_Time * 5), origin.xz - mesh.position.xz) + mesh.position.xz;
    }

    return origin;
}

//Inspired by this video: https://www.youtube.com/watch?v=VaYyPTw0V84
float4 CreateTextures(Ray ray)
{
    float GlobalDist = MaxDist;
    float3 GlobalColor = 0;

    float3 XY = 0;
    float3 XZ = 0;
    float3 YZ = 0;

    for (int x = 0; x < MeshCount; x++)
    {
        Ray NewRay = ray;

        Mesh mesh = Meshes[x];

        NewRay.origin = Transformations(NewRay.origin, mesh);

        float MeshDist = GetMeshSDF(mesh, NewRay.origin);

        float3 HitPoint = NewRay.origin + NewRay.direction * MeshDist;
        float3 normals = CalculateNormals(HitPoint - NewRay.direction * Epsilon);

        if (mesh.meshID == 2)
        {
            XY = _PlanetHeightTexture.SampleLevel(sampler_PlanetHeightTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
            XZ = _PlanetHeightTexture.SampleLevel(sampler_PlanetHeightTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
            YZ = _PlanetHeightTexture.SampleLevel(sampler_PlanetHeightTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

            normals = abs(normals);

            float3 DisplacementTexture = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);

            MeshDist -= DisplacementTexture.z * 0.12f;
        }
        else if (mesh.meshID > 2)
        {
            XY = _PlanetaryRingHeightTexture.SampleLevel(sampler_PlanetaryRingHeightTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
            XZ = _PlanetaryRingHeightTexture.SampleLevel(sampler_PlanetaryRingHeightTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
            YZ = _PlanetaryRingHeightTexture.SampleLevel(sampler_PlanetaryRingHeightTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

            normals = abs(normals);

            float3 DisplacementTexture = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);

            //Add smoothstep here

            MeshDist -= DisplacementTexture.z * 0.08f;
        }

        if (MeshDist < GlobalDist)
        {
            HitPoint = NewRay.origin + NewRay.direction * MeshDist;
            normals = CalculateNormals(HitPoint - NewRay.direction * Epsilon);

            if (mesh.meshID < 2)
            {
                float noise = StarNoise(float4(HitPoint, _Time * 0.8f));
                float spots = mix(1, snoise(float4(HitPoint * 0.3f, _Time * 0.6f)), 0.65f);
                float b = max(noise * spots, 0);

                b = b * 4.0f + 1;

                GlobalColor = BrightnessToColor(b, mesh.meshID);
            }
            else if (mesh.meshID == 2)
            {
                XY = _PlanetTexture.SampleLevel(sampler_PlanetTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
                XZ = _PlanetTexture.SampleLevel(sampler_PlanetTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
                YZ = _PlanetTexture.SampleLevel(sampler_PlanetTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

                normals = abs(normals);

                float3 col = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);

                XY = _PlanetNormalTexture.SampleLevel(sampler_PlanetNormalTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
                XZ = _PlanetNormalTexture.SampleLevel(sampler_PlanetNormalTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
                YZ = _PlanetNormalTexture.SampleLevel(sampler_PlanetNormalTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

                normals = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);
                normals = saturate(dot(normals, _DirectionalLight.xyz));

                GlobalColor = col * normals;
            }
            else if (mesh.meshID > 2)
            {
                XY = _PlanetaryRingTexture.SampleLevel(sampler_PlanetaryRingTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
                XZ = _PlanetaryRingTexture.SampleLevel(sampler_PlanetaryRingTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
                YZ = _PlanetaryRingTexture.SampleLevel(sampler_PlanetaryRingTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

                normals = abs(normals);

                float3 col = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);

                XY = _PlanetaryRingNormalTexture.SampleLevel(sampler_PlanetaryRingNormalTexture, HitPoint.xy * 0.5f + 0.5f, 0).xyz;
                XZ = _PlanetaryRingNormalTexture.SampleLevel(sampler_PlanetaryRingNormalTexture, HitPoint.xz * 0.5f + 0.5f, 0).xyz;
                YZ = _PlanetaryRingNormalTexture.SampleLevel(sampler_PlanetaryRingNormalTexture, HitPoint.yz * 0.5f + 0.5f, 0).xyz;

                normals = (XY * normals.z) + (XZ * normals.y) + (YZ * normals.x);
                normals = saturate(dot(normals, _DirectionalLight.xyz));

                GlobalColor = col * normals;
            }

            GlobalDist = MeshDist;
        }
    }

    return float4(GlobalColor, GlobalDist);
}

float CalculateHardShadows(Ray ray, float DistToShadePoint)
{
    float rayDist = 0;
    int marchSteps = 0;
    float shadowIntensity = 0.2f;
    float brightness = 1;

    while (rayDist < DistToShadePoint)
    {
        marchSteps++;
        float dist = DistToScene(ray.origin);

        if (dist <= Epsilon)
            return shadowIntensity;

        brightness = min(brightness, dist * 200);

        ray.origin += ray.direction * dist;
        rayDist += dist;
    }

    return shadowIntensity + (1 - shadowIntensity) * brightness;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width, height;
    Result.GetDimensions(width, height);

    float2 uv = id.xy / float2(width, height) * 2 - 1;
    float RayDist = 0;

    Ray ray = CreateCameraRay(uv);
    int MarchSteps = 0;

    while (RayDist < MaxDist)
    {
        MarchSteps++;

        float dist = DistToScene(ray.origin);

        float4 Output = CreateTextures(ray);

        float3 Textures = Output.xyz;
        dist = Output.w;

        if (dist <= Epsilon)
        {
            //Lighting
            float3 HitPoint = ray.origin + ray.direction * dist;
            float3 normals = CalculateNormals(HitPoint - ray.direction * Epsilon);
            float3 Lighting = saturate(dot(normals, _DirectionalLight.xyz) * -1) * _DirectionalLight.w;

            //Shadows
            float3 offsetPos = HitPoint + normals * 0.05f;
            float3 dirToLight = -_DirectionalLight;

            ray.origin = offsetPos;
            ray.direction = dirToLight;

            float distToLight = MaxDist;
            float Shadows = CalculateHardShadows(ray, distToLight);

            Result[id.xy] = float4(Textures /** Lighting * Shadows*/, 1);

            break;
        }
        else
        {
            float theta = acos(ray.direction.y) / -PI;
            float phi = atan2(ray.direction.x, -ray.direction.z) / -PI * 0.5f;
            Result[id.xy] = float4(_SkyboxTexture.SampleLevel(sampler_SkyboxTexture, float2(phi, theta), 0).xyz, 1);
        }

        ray.origin += ray.direction * dist;
        RayDist += dist;
    }
}
